#!/usr/bin/perl
# POD {{{
=head1 NAME

xbmc-cli - Simple script to control a remote XBMC instance

=head1 SYNOPSIS

	xbmc-cli pause
	xbmc-cli volup
	xbmc-cli voldown
	xbmc-cli debug

=head1 DESCRIPTION

A command line tool for the manipulation of remote XBMC instances.

Original API docs - http://wiki.xbmc.org/index.php?title=HTTP_API

=head1 COMMANDS

=over

=item B<xbmc-cli debug>

Show information about what server would be used and various program options.

=item B<xbmc-cli pause>

Send the play / pause command to the remote XBMC instance. If the media is playing - it will pause, if its not playing - it will unpause.

=item B<xbmc-cli voldown>

Turn the volume down.

=item B<xbmc-cli volup>

Turn the volume up.

=back

=head1 FILES

	$HOME/.xbmc-cli.ini

=over

=item B<$HOME/.xbmc-cli.ini>

The default config file used by xbmc-cli to specify the remote XBMC server.

An example ini file can be found in the docs folder.

=back

=head1 BUGS

Quite probably.

Please report to the author when found.

=head1 AUTHOR

Matt Carter <m@ttcarter.com>

=cut
# }}} POD
# Functions {{{
sub fatal {
	# Print an error message and fatally die
	print STDERR @_, "\n";
	exit 1;
}
# }}}
# Header {{{
package xbmccli;
use feature say;
our $VERSION = '0.1.0';
use LWP::Simple qw/get/;
use  Config::INI::Reader;
# }}}

# Config reader {{{
my $server;
my $port;
my $volstep = 10;
my $options = Config::INI::Reader->read_file("$ENV{HOME}/.xbmc-cli.ini");
$server = $options->{server}->{hostname} or fatal('No server specified in .xbmc-cli.ini');
$port = $options->{server}->{port} or fatal('No port specified in .xbmc-cli.ini');
$volstep = $options->{options}->{volstep} if $options->{options}->{volstep};
# }}}

$_ = lc pop;

if (/debug/) {
	say "Using server: http://$server:$port";
	say "Volstep: $volstep";
} elsif (my($direction) = (/vol(up|down)/)) {
	my $volume = get("http://$server:$port/xbmcCmds/xbmcHttp?command=GetVolume");
	fatal("Cant get current volume") unless (($volume) = ($volume =~ m/<li>([0-9]+)/g));
	$volume += ($direction eq 'up') ? $volstep : 0 - $volstep;
	get("http://$server:$port/xbmcCmds/xbmcHttp?command=SetVolume($volume)");
} elsif (/pause/) {  # Passthough commands
	$_ = ucfirst;
	get("http://$server:$port/xbmcCmds/xbmcHttp?command=$_");
}
exit 0;
